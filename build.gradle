defaultTasks 'test'

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'application'

project.ext {
    jdkVersion = JavaVersion.VERSION_1_7
    sourceCompatibility = jdkVersion
    neo4jVersion = "2.0.0-RC1"
    jerseyVersion = "1.9"
    junitVersion = "4.11"
}

repositories {
        mavenCentral()
        maven { url "http://m2.neo4j.org/content/repositories/releases/" }
    }

subprojects {
    apply plugin: "java"
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    repositories {
        mavenCentral()
        maven { url "http://m2.neo4j.org/content/repositories/releases/" }
    }
    idea {
        module {
            downloadJavadoc = true
        }
    }
    eclipse {
        classpath {
            downloadJavadoc = true
        }
    }
}

idea {
    project {
        jdkName = jdkVersion
        languageLevel = jdkVersion
        ipr {
            withXml { provider ->
                provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

task snippets << {
    ant.replaceregexp(match:"// SNIPPET_START(.+?)// SNIPPET_END",
            replace:"",
            flags:"gis") {
        ant.fileset dir:"koan/src/test/java", includes:"**/*.java"
    }
}

// gradle run invokes the shell
dependencies {
    runtime group: 'org.neo4j', name: 'neo4j-shell', version: neo4jVersion
    runtime group: 'org.neo4j', name: 'neo4j-kernel', version: neo4jVersion
}
mainClassName = 'org.neo4j.shell.StartClient'

/*TODO: 
* implement task shell
* REST Client: cypher + transactional endpoint
*/
