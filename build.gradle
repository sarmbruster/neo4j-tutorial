defaultTasks 'test'

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'application'

project.ext {
    jdkVersion = JavaVersion.VERSION_1_7
    sourceCompatibility = jdkVersion
    neo4jVersion = "2.0.1"
    jerseyVersion = "1.9"
    junitVersion = "4.11"
}

repositories {
    maven { url "file://maven-repo-local" }
}

subprojects {
    apply plugin: "java"
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    repositories {
        mavenCentral()
        maven { url "http://m2.neo4j.org/content/repositories/releases/" }
    }
    idea {
        module {
            downloadJavadoc = true
        }
    }
    eclipse {
        classpath {
            downloadJavadoc = true
        }
    }
}

idea {
    project {
        jdkName = jdkVersion
        languageLevel = jdkVersion
        ipr {
            withXml { provider ->
                provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
            }
        }
    }
}

task snippets << {
    ant.replaceregexp(match:"// SNIPPET_START(.+?)// SNIPPET_END",
            replace:"",
            flags:"gis") {
        ant.fileset dir:"koan/src/test/java", includes:"**/*.java"
    }
}

// gradle run invokes the shell
dependencies {
    runtime group: 'org.neo4j', name: 'neo4j-shell', version: neo4jVersion
    runtime group: 'org.neo4j', name: 'neo4j-kernel', version: neo4jVersion
}
mainClassName = 'org.neo4j.shell.StartClient'

/*TODO: 
* implement task shell
* REST Client: cypher + transactional endpoint
*/


// make sure gradle wrapper does not need to download gradle from internet
wrapper {
    //archiveBase = "PROJECT"
    //distributionBase = "PROJECT"
    distributionUrl = "gradle-${gradleVersion}-bin.zip"
    gradleVersion = "1.11"
}

wrapper.doFirst {
   ant.get(src:'http://services.gradle.org/distributions/gradle-1.11-bin.zip', dest: 'gradle/wrapper', usetimestamp: true)
}
