defaultTasks 'test'

apply plugin: 'idea'
apply plugin: 'eclipse'

project.ext {
    jdkVersion = JavaVersion.VERSION_1_7
    sourceCompatibility = jdkVersion
    neo4jVersion = "1.9.4"
    jerseyVersion = "1.9"
    junitVersion = "4.11"
}

subprojects {
    apply plugin: "java"
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    repositories {
        mavenCentral()
        maven { url "http://m2.neo4j.org/content/repositories/releases/" }
    }
}

idea {
    project {
        jdkName = jdkVersion
        languageLevel = jdkVersion
        ipr {
            withXml { provider ->
                provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

task snippets << {
    ant.replaceregexp(match:"// SNIPPET_START(.+?)// SNIPPET_END",
            replace:"",
            flags:"gis") {
        ant.fileset dir:"koan/src/test/java", includes:"**/*.java"
    }
}
